apply plugin: 'com.android.application'
apply plugin: 'idea'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null;
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "edu.rutgers.css.Rutgers"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 41
        versionName "4.1"
        resConfigs "en"
        testApplicationId "edu.rutgers.css.Rutgers.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        alpha {
            versionName getVersionName()
            applicationId "edu.rutgers.css.Rutgers.alpha"
        }
        beta {
            versionName getVersionName()
            applicationId "edu.rutgers.css.Rutgers.beta"
        }
        production {
            versionName "4.1"
            applicationId "edu.rutgers.css.Rutgers"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

repositories {
    mavenCentral()
    jcenter()

    // For inlcuding the showcaseview AAR
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(':PagerSlidingTabStrip')
    compile project(':ListViewAnimations:lib-core')
    compile project(':ListViewAnimations:lib-core-slh')
    compile project(':ListViewAnimations:lib-manipulation')
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.google.android.gms:play-services-location:9.2.1'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'se.emilsjolander:stickylistheaders:2.6.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'jp.wasabeef:picasso-transformations:1.3.1'
    compile 'org.osmdroid:osmdroid-android:5.1@aar'
    provided 'org.projectlombok:lombok:1.16.6'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.1'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
//    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'com.android.support:support-annotations:24.1.1'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
