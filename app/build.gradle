//plugins {
//    id "me.tatarka.retrolambda" version "3.3.1"
//}

apply plugin: 'com.android.application'
apply plugin: 'idea'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null;
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId "edu.rutgers.css.Rutgers"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 51
        versionName "4.1"
        resConfigs "en"
        testApplicationId "edu.rutgers.css.Rutgers.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        jackOptions {
            enabled true
            jackInProcess false
        }
    }

    // Jack needs a lot of memory for some reason
    dexOptions {
        javaMaxHeapSize "4G"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        alpha {
            versionName getVersionName()
            applicationId "edu.rutgers.css.Rutgers.alpha"
        }
        beta {
            versionName getVersionName()
            applicationId "edu.rutgers.css.Rutgers.beta"
        }
        production {
            versionName "4.1"
            applicationId "edu.rutgers.css.Rutgers"
        }
    }

    packagingOptions {
        exclude 'rome-utils-*.jar'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    jcenter()

    // For inlcuding the showcaseview AAR
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')

    // Support libs
    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:transition:25.0.1'
    compile 'com.android.support:cardview-v7:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    // Rx
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.trello:rxlifecycle:0.7.0'
    compile 'com.trello:rxlifecycle-android:0.7.0'
    compile 'com.trello:rxlifecycle-components:0.7.0'

    // Square
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile ('com.squareup.retrofit2:converter-simplexml:2.1.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    // square can't update their libs
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    // Misc
    compile 'com.google.android.gms:play-services-location:9.8.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'jp.wasabeef:picasso-transformations:1.3.1'
    compile 'jp.wasabeef:recyclerview-animators:2.2.4'
    compile 'com.rometools:rome:1.7.0'
    compile 'com.rometools:rome-modules:1.7.0'
    compile 'io.github.yavski:fab-speed-dial:1.0.4'
    compile 'com.bignerdranch.android:expandablerecyclerview:2.1.1'
    compile 'com.synnapps:carouselview:0.0.9'
    compile 'com.afollestad:sectioned-recyclerview:0.2.3'

    // Test
//    testCompile 'junit:junit:4.12'
//    testCompile 'org.mockito:mockito-core:1.10.19'
//    testCompile 'org.hamcrest:hamcrest-library:1.1'
//    androidTestCompile 'com.android.support.test:runner:0.4'
//    androidTestCompile 'com.android.support.test:rules:0.4'
//    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
////    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
//    androidTestCompile 'com.android.support:support-annotations:24.2.0'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
